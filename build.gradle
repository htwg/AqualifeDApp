apply plugin: "java"
apply plugin: "idea"

repositories {
    mavenCentral()
}

dependencies {
    compile group: "org.web3j", name: "core", version: "3.4.0"
}

sourceSets {
    main.java.srcDir "src"
}

jar.baseName "AqualifeDApp"

jar {
    // specify main class
    manifest {
        attributes "Main-Class": "aqualife.client.Aqualife"
    }

    // include resources (images, ...)
    into("aqualife.client/resources") {
        from("src/aqualife.client/resources")
    }

    // include all external jar libraries (dependencies) and exclude signature files
    // to avoid "Invalid signature file digest for Manifest main attributes" exception
    // when executing the jar file
    from(configurations.compile.collect { entry -> zipTree(entry) }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }

    // specify output directory for jar file
    destinationDir = file("$rootDir/jar/")
}

idea {
    project {
        //if you want to set specific jdk and language level
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}